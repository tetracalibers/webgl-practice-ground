---
title: "HSV色空間の断面"
---

import Canvas from "@/canvas/hsv-color-space-bg/canvas.astro"

<Canvas />

## hsv2rgb のロジックを追う

![](/webgl-practice-ground/articles/hsv-color-space-bg/hue-circle.png)

### vec3(color.x)

```glsl
vec3 hsv2rgb(vec3 color) {
  return vec3(color.x);
}
```

- r = g = b であることから、Red も Green も Blue も均等に混ざり合って、グレースケールになる。

![](/webgl-practice-ground/articles/hsv-color-space-bg/00.png)

### vec3(color.x \* 0.6)

```glsl
vec3 hsv2rgb(vec3 color) {
  // Hueを[0, 1]から[0, 6]へスケール
  float hue = color.x * 6.0;

  return vec3(hue);
}
```

- 原点から 60° 分色がつく。

![](/webgl-practice-ground/articles/hsv-color-space-bg/01.png)

### vec3(0.0, 4.0, 2.0)

```glsl
vec3 hsv2rgb(vec3 color) {
  // Hueを[0, 1]から[0, 6]へスケール
  float hue = color.x * 6.0;

  return vec3(0.0, 4.0, 2.0);
}
```

- 色は(0.0, 4.0, 2.0) = (0.0, 1.0, 1.0) = (0, 255, 255)

![](/webgl-practice-ground/articles/hsv-color-space-bg/02.png)

### hue + vec3(0.0, 4.0, 2.0)

```glsl
vec3 hsv2rgb(vec3 color) {
  // Hueを[0, 1]から[0, 6]へスケール
  float hue = color.x * 6.0;

  return hue + vec3(0.0, 4.0, 2.0);
}
```

![](/webgl-practice-ground/articles/hsv-color-space-bg/03.png)

### mod(hue + vec3(0.0, 4.0, 2.0), 6.0)

```glsl
vec3 hsv2rgb(vec3 color) {
  // Hueを[0, 1]から[0, 6]へスケール
  float hue = color.x * 6.0;

  vec3 m = mod(hue + vec3(0.0, 4.0, 2.0), 6.0);

  return m;
}
```

- water (0, 255, 255) ... hue が 0 の場合、m = (0.0, 4.0, 2.0)
- pink (255, 0, 255) ... hue が 2 の場合、m = (2.0, 0.0, 4.0)
- yellow (255, 255, 0) ... hue が 4 の場合、m = (4.0, 2.0, 0.0)

![](/webgl-practice-ground/articles/hsv-color-space-bg/04.png)

### abs(mod(hue + vec3(0.0, 4.0, 2.0), 6.0) - 3.0)

```glsl
vec3 hsv2rgb(vec3 color) {
  // Hueを[0, 1]から[0, 6]へスケール
  float hue = color.x * 6.0;

  vec3 m = mod(hue + vec3(0.0, 4.0, 2.0), 6.0);
  vec3 a = abs(m - 3.0);

  return a;
}
```

- hue = 0 の場合
  - a = abs(-3.0, 1.0, -1.0) = (3.0, 1.0, 1.0) = white
- hue = 2 の場合
  - a = abs(-1.0, -3.0, 1.0) = (1.0, 3.0, 1.0) = white
- hue = 4 の場合
  - a = abs(1.0, -1.0, -3.0) = (1.0, 1.0, 3.0) = white

![](/webgl-practice-ground/articles/hsv-color-space-bg/05.png)

### abs(mod(hue + vec3(0.0, 4.0, 2.0), 6.0) - 3.0) - 1.0

```glsl
vec3 hsv2rgb(vec3 color) {
  // Hueを[0, 1]から[0, 6]へスケール
  float hue = color.x * 6.0;

  vec3 m = mod(hue + vec3(0.0, 4.0, 2.0), 6.0);
  vec3 a = abs(m - 3.0);

  return a - 1.0;
}
```

- hue = 0 の場合
  - a - 1.0 = (3.0, 1.0, 1.0) - 1.0 = (2.0, 0.0, 0.0) = red
- hue = 2 の場合
  - a - 1.0 = (1.0, 3.0, 1.0) - 1.0 = (0.0, 2.0, 0.0) = green
- hue = 4 の場合
  - a - 1.0 = (1.0, 1.0, 3.0) - 1.0 = (0.0, 0.0, 2.0) = blue

![](/webgl-practice-ground/articles/hsv-color-space-bg/06.png)

### clamp(abs(mod(hue + vec3(0.0, 4.0, 2.0), 6.0) - 3.0) - 1.0, 0.0, 1.0)

```glsl
vec3 hsv2rgb(vec3 color) {
  // Hueを[0, 1]から[0, 6]へスケール
  float hue = color.x * 6.0;

  vec3 m = mod(hue + vec3(0.0, 4.0, 2.0), 6.0);
  vec3 a = abs(m - 3.0);
  vec3 rgb = clamp(a - 1.0, 0.0, 1.0);

  return rgb;
}
```

見た目は変わらず。

- hue = 0 の場合 (1.0, 0.0, 0.0) = red
- hue = 2 の場合 (0.0, 1.0, 0.0) = green
- hue = 4 の場合 (0.0, 0.0, 1.0) = blue

### mix(vec3(1.0), rgb, color.y)

```glsl
vec3 hsv2rgb(vec3 color) {
  // Hueを[0, 1]から[0, 6]へスケール
  float hue = color.x * 6.0;

  vec3 m = mod(hue + vec3(0.0, 4.0, 2.0), 6.0);
  vec3 a = abs(m - 3.0);
  vec3 rgb = clamp(a - 1.0, 0.0, 1.0);

  // 白とrgbを彩度（動径）に沿って補間
  return mix(vec3(1.0), rgb, color.y);
}
```

- color.y は彩度 s = 円錐の半径

![](/webgl-practice-ground/articles/hsv-color-space-bg/07.png)

### color.z \* mix(vec3(1.0), rgb, color.y)

```glsl
vec3 hsv2rgb(vec3 color) {
  // Hueを[0, 1]から[0, 6]へスケール
  float hue = color.x * 6.0;

  vec3 m = mod(hue + vec3(0.0, 4.0, 2.0), 6.0);
  vec3 a = abs(m - 3.0);
  vec3 rgb = clamp(a - 1.0, 0.0, 1.0);

  // 白とrgbを彩度（動径）に沿って補間したものに明度をかける
  return color.z * mix(vec3(1.0), rgb, color.y);
}
```

![](/webgl-practice-ground/articles/hsv-color-space-bg/08.png)

### 参考

- [HSB(HSV)から if 文を使わずに RGB へ変換する](https://sites.google.com/site/bknobiboroku/programming-tips/wpf/Csharp_wpf_HSB_to_RGB)
- [RGB -> HSV 変換式を中学数学でガチ導出する](https://zenn.dev/tetracalibers/articles/78474ee3e8678e)
